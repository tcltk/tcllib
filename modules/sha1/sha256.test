# -*- tcl -*-
# sha256.test:  tests for the sha256 commands
#
# This file contains a collection of tests for one or more of the Tcl
# built-in commands.  Sourcing this file into Tcl runs the tests and
# generates output for errors.  No output means no errors were found.
#
# RCS: @(#) $Id: sha256.test,v 1.1 2005/02/22 10:15:24 patthoyts Exp $

# -------------------------------------------------------------------------
# Initialize the test package
#
if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest
    namespace import ::tcltest::*
}

# -------------------------------------------------------------------------
# Ensure we test _this_ local copy and one installed somewhere else.
#
package forget sha256
catch {namespace delete ::sha2}
if {[catch {source [file join [file dirname [info script]] sha256.tcl]} msg]} {
    puts "skipped [file tail [info script]]: $msg"
    return
}

# -------------------------------------------------------------------------
# Setup any constraints
#

# -------------------------------------------------------------------------
# Now the package specific tests....
# -------------------------------------------------------------------------

puts "- sha256 [package present sha256] (pure Tcl)"


test sha256-1.0 {sha256 usage} {
    catch {::sha2::sha256} result
    set result
} "wrong # args: should be \"::sha2::sha256 ?-hex|-bin? -filename file | -channel channel | string\""

test sha224-1.0 {sha224 usage} {
    catch {::sha2::sha224} result
    set result
} "wrong # args: should be \"::sha2::sha224 ?-hex|-bin? -filename file | -channel channel | string\""


# FIPS 180-2 test vectors SHA-256
set vectors \
    [list \
         1 "abc" \
         "ba7816bf8f01cfea414140de5dae2223b00361a396177a9cb410ff61f20015ad" \
         2 "abcdbcdecdefdefgefghfghighijhijkijkljklmklmnlmnomnopnopq" \
         "248d6a61d20638b8e5c026930c3e6039a33ce45964ff2167f6ecedd419db06c1" \
         3 [string repeat a 1000000] \
         "cdc76e5c9914fb9281a1c7e284d73e67f1809a48a497200e046d39ccc7112cd0" ]

foreach {n msg hash} $vectors {
    test sha256-2.$n {FIPS-180-2 test vectors for SHA-256} {
	list [catch {::sha2::sha256 $msg} r] $r
    } [list 0 $hash]
}

# FIPS 180-2 test vectors SHA-224
set vectors \
    [list \
         1 "abc" \
         "23097d223405d8228642a477bda255b32aadbce4bda0b3f7e36c9da7" \
         2 "abcdbcdecdefdefgefghfghighijhijkijkljklmklmnlmnomnopnopq" \
         "75388b16512776cc5dba5da1fd890150b0c6455cb4f58b1952522525" \
         2 [string repeat a 1000000] \
         "20794655980c91d8bbb4c1ea97618a4bf03f42581948b2ee4ee7ad67" ]

foreach {n msg hash} $vectors {
    test sha224-2.$n {FIPS-180-2 test vectors for SHA-224} {
	list [catch {::sha2::sha224 $msg} r] $r
    } [list 0 $hash]
}

# -------------------------------------------------------------------------

::tcltest::cleanupTests

# -------------------------------------------------------------------------
# Local Variables:
#   mode: tcl
#   indent-tabs-mode: nil
# End:

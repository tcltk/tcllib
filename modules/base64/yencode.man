[manpage_begin yencode n 1.1.4]
[keywords encoding]
[keywords ydecode]
[keywords yEnc]
[keywords yencode]
[copyright {2002, Pat Thoyts}]
[moddesc   {Text encoding & decoding binary data}]
[titledesc {Y-encode/decode binary data}]
[category  {Text processing}]
[require Tcl "8.5 9"]
[require yencode [opt 1.1.4]]
[description]
[para]

This package provides a Tcl-only implementation of the yEncode file
encoding used for Usenet messages. This encoding packs binary data
into a format that requires an 8-bit clean transmission layer but that
escapes characters special to the [term NNTP] posting protocols. See
[uri http://www.yenc.org] for the encoding’s details.

[list_begin definitions]

[call [cmd ::yencode::encode] [arg bstring]]

Returns a yEncoded version of the binary string [arg bstring] as
its result.

[para]
The command will throw an error if [arg bstring] is neither a binary
string, nor a string containing only 7-bit ASCII.

[call [cmd ::yencode::decode] [arg "estring"]]

Returns a binary string that has been yEncode-decoded from the
[arg estring].

[call [cmd ::yencode::yencode] \
  [opt "[option -name] [arg string]"] \
  [opt "[option -line] [arg integer]"] \
  [opt "[option -crc32] [arg boolean]"] \
  "([option -file] [arg filename] | [opt [option --]] [arg bstring])"]

Returns a wrapped yEncoded version of the data in the file specified by
[option -file] [arg filename] or by the binary string [arg bstring] as
its result.

[para]
Each wrapped yEncoded result consists of a header, body, and trailer.
The header is a line starting with [const =ybegin] followed by a line
length of form [const line=][emph length], the size of the original data
(in bytes) of form [const size=][emph size], and the name of the
original file of form [const name=][emph filename], all space-separated.
The body is the yEncoded data. The trailer is a line starting with
[const =yend] followed by the size of the original data of form
[const size=][emph size], and optionally (but recommended), a CRC32
checksum value of form [const crc32=][emph checksum], all
space-separated.

[call [cmd ::yencode::ydecode] \
  "([option -file] [arg filename] | [opt [option --]] [arg estring])"]

Returns a list of 3-element lists of filename, file size,
yEncode-decoded bytes, from the file specified by [option -file]
[arg filename] or from the [arg estring].

[list_end]

[subsection Options]

[list_begin definitions]

[def "[option -file] [arg filename]"]

Cause the [cmd yencode] or [cmd ydecode] commands to read their data
from the named file rather that taking a binary string parameter.

[def "[option -name] [arg string]"]

The yEncoded data header line contains the original file name to be
used when unpacking the data. Use this option to change this from the
default of "default.bin".

[def "[option -line] [arg integer]"]

The yEncoded header line specifies the line length used for the
encoding, and which defaults to 128. Use this option to set a different
line length. Note that NNTP imposes a 1000 character line length limit
and some systems may have trouble with more than 255 characters per
line.

[def "[option -crc32] [arg boolean]"]

When encoding this package puts a cyclic redundancy
check (CRC) value in the trailer as recommended in the yEncode
specification. This can be prevented by using this option and passing a
false value for [arg boolean], e.g., [const 0].

[list_end]

[section Examples]

[emph {The yEncoded data is not shown in the examples because it is just
raw bytes.}]

[para]
This example shows how to yEncode and yEncode-decode a Tcl string, taking
account of the fact that the yEncode commands work in terms of binary
strings.

[para]
[example {
const UTF8_LINE "Δ÷ “Utf-8” ♞ℤ"
set bytes [encoding convertto utf-8 $UTF8_LINE]
set ency [::yencode::encode $bytes]
set decy [::yencode::decode $ency]
set line [encoding convertfrom utf-8 $decy]
puts "[expr {$UTF8_LINE eq $line}]"
=>
1
}]

If the original string is 7-bit ASCII the conversions to and from raw
bytes using the built-in [cmd encoding] command are not needed. For example:

[para]
[example {
const ASCII_LINE "! 7-bit ASCII {~^}"
set ency [::yencode::encode $ASCII_LINE]
set decy [::yencode::decode $ency]
set line [encoding convertfrom utf-8 $decy]
puts "[expr {$ASCII_LINE eq $line}]"
=>
1
}]

This example shows how to create a yEncoded byte string and write it to
a file.

[para]
[example {
set bytes [encoding convertto utf-8 $UTF8_LINE]
set ency [::yencode::yencode -name test.y $bytes]
writeFile test.y binary $ency
puts $ency
=>
=ybegin line=128 size=23 name=test.y
    … raw binary data elided …
=yend size=23 crc32=fc01071
}]

This example shows how to read a file containing one or more yEncoded
files.

[para]
[example {
foreach lst [::yencode::ydecode $ency] {
    lassign $lst name size decy
    set line [encoding convertfrom utf-8 $decy]
    puts "name=$name size=$size line='$line'"
}
=>
name=test.y size=23 line='Δ÷ “Utf-8” ♞ℤ'
}]

[section References]

[list_begin enum]
[enum] [uri http://www.yenc.org/yenc-draft.1.3.txt]
[list_end]

[vset CATEGORY base64]
[include ../common-text/feedback.inc]
[manpage_end]

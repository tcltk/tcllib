[manpage_begin uuencode n 1.1.6]
[keywords encoding]
[keywords uuencode]
[copyright {2002, Pat Thoyts}]
[moddesc   {Text encoding & decoding binary data}]
[titledesc {UU-encode/decode binary data}]
[category  {Text processing}]
[require Tcl "8.5 9"]
[require uuencode [opt 1.1.6]]
[description]
[para]

This package provides a Tcl-only implementation of the
[uri https://www.man7.org/linux/man-pages/man1/uuencode.1p.html \
[syscmd uuencode(1p)]] and
[uri https://www.man7.org/linux/man-pages/man1/uudecode.1p.html \
[syscmd uudecode(1p)]] commands.
This encoding packs binary data into printable 7-bit ASCII characters.

[list_begin definitions]

[call [cmd ::uuencode::encode] [arg bstring]]

Returns a uuencoded version of the binary string [arg bstring] as
its result.

[para]
All the given bytes are encoded even if they exceed the uuencode maximum
line length. If the number of input bytes is not a multiple of 3 then
the string is padded with additional [const \\x00] NUL bytes.

[para]
The command will throw an error if [arg bstring] is neither a binary
string, nor a string containing only 7-bit ASCII.

[call [cmd ::uuencode::decode] [arg estring]]

Returns a binary string that has been uuencode-decoded from the
[arg estring], including any padding [const \\x00] NUL bytes at the end.

[call [cmd ::uuencode::uuencode] [opt "[option -name] [arg string]"] \
[opt "[option -mode] [arg octal]"] "([option -file] [arg filename] | \
[opt [option --]] [arg bstring])"]

Returns a wrapped uuencoded version of the data in the file specified by
[option -file] [arg filename] or by the binary string [arg bstring] as
its result.

[para]
Each wrapped uuencoded result consists of a header, body, and footer. The
header is a line starting with [const begin] followed by a suggested
file mode and suggested filename. The body consists of lines of
uuencoded data, each prefixed with a line length character. The footer
is [const end] on its own line. See
[uri https://www.man7.org/linux/man-pages/man1/uuencode.1p.html \
[syscmd uuencode(1)]].

[call [cmd ::uuencode::uudecode] "([option -file] [arg filename] | \
[opt [option --]] [arg estring])"]

Returns a list of 3-element lists of filename, file mode,
uuencode-decoded bytes, from the file specified by [option -file]
[arg filename] or from the [arg estring].

[list_end]

[subsection Options]

[list_begin definitions]

[def "[option -file] [arg filename]"]

Cause the [cmd uuencode] or [cmd uudecode] commands to read their data
from the named file rather that taking a binary string parameter.

[def "[option -name] [arg string]"]

The uuencoded data header line contains the suggested file name to be
used when unpacking the data. Use this option to change this from the
default of "data.dat".

[def "[option -mode] [arg octal]"]

The uuencoded data header line contains a suggested permissions bit
pattern expressed as an octal string. To change the default of 0644
you can set this option. For instance, 0755 would be suitable for an
executable. See
[uri https://www.man7.org/linux/man-pages/man1/chmod.1.html \
[syscmd chmod(1)]].

[list_end]

[section Examples]

The examples use the subcommand [cmd {string trimright}] to trim any
[const \\x00] NUL bytes that may have been added during encoding.
[emph {Note however, that if the original binary strings might end
with}] [const \\x00] NUL [emph {bytes, a more sophisticated approach
will be needed.}]

[para]
This example shows how to uuencode and uudecode a Tcl string, taking
account of the fact that the uuencode commands work in terms of binary
strings.

[para]
[example {
const UTF8_LINE "Δ÷ “Utf-8” ♞ℤ"
set bytes [encoding convertto utf-8 $UTF8_LINE]
set encuu [::uuencode::encode $bytes]
set decuu [string trimright [::uuencode::decode $encuu] \x00]
set line [encoding convertfrom utf-8 $decuu]
puts "uuencode='$encuu' [expr {$UTF8_LINE eq $line}]"
=>
uuencode='SI3#MR#B@)Q5=&8M..*`G2#BF9[BA*0`' 1
}]

If the original string is 7-bit ASCII the conversions to and from raw
bytes using the built-in [cmd encoding] command are not needed. For example:

[para]
[example {
const ASCII_LINE "! 7-bit ASCII {~^}"
set encuu [::uuencode::encode $ASCII_LINE]
set decuu [string trimright [::uuencode::decode $encuu] \x00]
set line [encoding convertfrom utf-8 $decuu]
puts "uuencode='$encuu' [expr {$ASCII_LINE eq $line}]"
=>
uuencode='(2`W+6)I="!!4T-)22![?EY]' 1
}]

This example shows how to create a uuencoded byte string and write it to
a file.

[para]
[example {
const UTF8_LINE "Δ÷ “Utf-8” ♞ℤ"
set bytes [encoding convertto utf-8 $UTF8_LINE]
set encuu [::uuencode::uuencode -name test.uu $bytes]
writeFile test.uu binary $encuu
puts $encuu
=>
begin 644 test.uu
7SI3#MR#B@)Q5=&8M..*`G2#BF9[BA*0`
`
end
}]

This example shows how to read a file containing one or more uuencoded
files.

[para]
[example {
foreach lst [::uuencode::uudecode -file test.uu] {
    lassign $lst name mode decuu
    set decuu [string trimright $decuu \x00]
    set line [encoding convertfrom utf-8 $decuu]
    puts "name=$name mode=$mode line='$line'"
}
=>
name=test.uu mode=644 line='Δ÷ “Utf-8” ♞ℤ'
}]

[vset CATEGORY base64]
[include ../common-text/feedback.inc]
[manpage_end]

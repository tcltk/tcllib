# time.test = Copyright (C) 2003 Pat Thoyts <patthoyts@users.sourceforge.net>
#
# Exercise the tcllib time package.
#
# $Id: time.test,v 1.1 2003/03/20 00:29:41 patthoyts Exp $

if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest
    namespace import -force ::tcltest::*
}

if {[catch {package require time}]} {
        puts "ERROR: failed to load time package. Skipping tests."
        ::tcltest::cleanupTests
        return
}

# -------------------------------------------------------------------------

set testScript tstsrv.tmp

proc createServerProcess {} {
    file delete -force $::testScript
    set f [open $::testScript w+]
    puts $f {
        proc ::srv {chan args} {
            if {[catch {
                set r [binary format I [expr {[clock seconds] + 2208988800}]]
                puts "connect on $chan from [fconfigure $chan -peername]"
                puts -nonewline $chan $r
                close $chan
            } msg]} {
                puts stderr "error: $msg"
            }
            set ::done 1
        }
        
        set s [socket -server ::srv 0]
        fconfigure $s -translation binary -buffering none -eofchar {}
        set port [lindex [fconfigure $s -sockname] 2]
        
        puts $port 
        flush stdout
        vwait ::done
        update
        exit
    }
    close $f

    set f [open |[list [::tcltest::interpreter] $::testScript] r]
    fconfigure $f -buffering line -blocking 1
    #after 500 {set _init 1} ; vwait _init
    return $f
}

# -------------------------------------------------------------------------

set token {}

test time-1.1 {time::gettime} {
    global token
    list [catch {
        set f [createServerProcess]
        gets $f port
        set token [::time::gettime -protocol tcp -port $port localhost]
        set r {}
    } msg] $msg    
} {0 {}}

test time-1.2 {time::status} {
    global token
    list [catch {time::status $token} m] $m
} {0 ok}

test time-1.2 {time::unixtime} {
    global token
    list [catch {
        set t [time::unixtime $token]
        string is integer -strict $t
    } m] $m
} {0 1}

test time-1.3 {time::cget} {
    global token
    list [catch {
        time::cget -port
    } m] $m
} {0 37}

test time-1.4 {time::cleanup} {
    global token
    list [catch {
        time::cleanup $token
    } m] $m
} {0 {}}

# -------------------------------------------------------------------------
file delete -force $::testScript
::tcltest::cleanupTests
return

#
# Local variables:
#   mode: tcl
#   indent-tabs-mode: nil
# End:

# ripemd128.test - Copyright (C) 2003 Pat Thoyts <patthoyts@users.sf.net>
#
# $Id: ripemd128.test,v 1.5 2005/02/17 23:30:31 patthoyts Exp $

# -------------------------------------------------------------------------
# Initialise the test package
#
if {[lsearch [namespace children] ::tcltest] == -1} {
    package require tcltest
    namespace import ::tcltest::*
}

# -------------------------------------------------------------------------
# Ensure we test _this_ local copy and one installed somewhere else.
#
package forget ripemd128
catch {namespace delete ::ripemd::ripemd128}
if {[catch {source [file join [file dirname [info script]] ripemd128.tcl]} msg]} {
    puts "skipped [file tail [info script]]: $msg"
    return
}

package require ripemd128

if {$::ripemd::::ripemd128::usetrf} {
    puts "- ripemd128 [package provide ripemd128] (Trf based)"
}
puts "- ripemd128 [package provide ripemd128] (pure Tcl)"

# -------------------------------------------------------------------------
# Setup any constraints
#

# -------------------------------------------------------------------------
# Now the package specific tests....
# -------------------------------------------------------------------------

# -------------------------------------------------------------------------

# Test vectors
#
set vectors {
    1 {}    {CDF26213A150DC3ECB610F18F6B38B46}
    2 {a}   {86BE7AFA339D0FC7CFC785E72F578D33}
    3 {abc} {C14A12199C66E4BA84636B0F69144C77}
    4 {message digest} {9E327B3D6E523062AFC1132D7DF9D1B8}
    5 {abcdefghijklmnopqrstuvwxyz} {FD2AA607F71DC8F510714922B371834E}
    6 {abcdbcdecdefdefgefghfghighijhijkijkljklmklmnlmnomnopnopq}
      {A1AA0689D0FAFA2DDC22E88B49133A06}
    7 {ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789}
      {D1E959EB179C911FAEA4624C60C5C702}
    8 {-} {CCE66016F96D83CBC625AD3970B9706E}
}
lappend vectors \
    9 [string repeat 1234567890 8] {3F45EF194732C2DBB2C4A2C769795FA3}

foreach {n msg hash} $vectors {
    test ripemd128-1.$n {RIPEMD-128 test strings} {
        ::ripemd::ripemd128 -hex -- $msg
    } $hash
}

# If our implementation is managing to use Trf then check the pure tcl
# version as well.
if {$::ripemd::ripemd128::usetrf} {
    set ::ripemd::ripemd128::usetrf 0
    foreach {n msg hash} $vectors {
        test ripemd128-tcl-1.$n {RIPEMD-128 test strings (pure-Tcl impl)} {
            ::ripemd::ripemd128 -hex -- $msg
        } $hash
    }
    set ::ripemd::ripemd128::usetrf 1
}

# -------------------------------------------------------------------------
# HMAC test vectors

set key [binary format H* 00112233445566778899aabbccddeeff]

set vectors {
    1 {}    {AD9DB2C1E22AF9AB5CA9DBE5A86F67DC}
    2 {a}   {3BF448C762DE00BCFA0310B11C0BDE4C}
    3 {abc} {F34EC0945F02B70B8603F89E1CE4C78C}
    4 {message digest} {E8503A8AEC2289D82AA0D8D445A06BDD}
    5 {abcdefghijklmnopqrstuvwxyz} {EE880B735CE3126065DE1699CC136199}
    6 {abcdbcdecdefdefgefghfghighijhijkijkljklmklmnlmnomnopnopq}
      {794DAF2E3BDEEA2538638A5CED154434}
    7 {ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789}
      {3A06EEF165B23625247800BE23E232B6}
}
lappend vectors \
    8 [string repeat 1234567890 8] {9A4F0159C0952DA43A8D466D46B0AF58}

foreach {n msg hash} $vectors {
    test ripemd128-2.$n {HMAC RIPEMD-128 test vectors} {
        ::ripemd::hmac128 -hex -key $key -- $msg
    } $hash
}

# If our implementation is managing to use Trf then check the pure tcl
# version as well.
if {$::ripemd::ripemd128::usetrf} {
    set ::ripemd::ripemd128::usetrf 0
    foreach {n msg hash} $vectors {
        test ripemd128-tcl-2.$n {HMAC RIPEMD-128 test vectors (pure-Tcl impl)} {
            ::ripemd::hmac128 -hex -key $key -- $msg
        } $hash
    }
    set ::ripemd::ripemd128::usetrf 1
}

# -------------------------------------------------------------------------
# HMAC test vectors - key 2

set key [binary format H* 0123456789abcdeffedcba9876543210]

set vectors {
    1 {}    {8931EEEE56A6B257FD1AB5418183D826}
    2 {a}   {DBBCF169EA7419D5BA7BD8EB3673FF2D}
    3 {abc} {2C4CD07D3162D6A0E338004D6B6FBC9A}
    4 {message digest} {75BFB25888F4BB77C77AE83AD0817447}
    5 {abcdefghijklmnopqrstuvwxyz} {B1B5DC0FCB7258758855DD1840FCDCE4}
    6 {abcdbcdecdefdefgefghfghighijhijkijkljklmklmnlmnomnopnopq}
      {670D0F7A697B18F1A8AB7D2A2A00DBC1}
    7 {ABCDEFGHIJKLMNOPQRSTUVWXYZabcdefghijklmnopqrstuvwxyz0123456789}
      {54E315FDB34A61C0475392E5C7852998}
}
lappend vectors \
    8 [string repeat 1234567890 8] {AD04354D8AA2A623E72E3594EE3535C0}

foreach {n msg hash} $vectors {
    test ripemd128-3.$n {HMAC RIPEMD-128 test vectors} {
        ::ripemd::hmac128 -hex -key $key -- $msg
    } $hash
}

# If our implementation is managing to use Trf then check the pure tcl
# version as well.
if {$::ripemd::ripemd128::usetrf} {
    set ::ripemd::ripemd128::usetrf 0
    foreach {n msg hash} $vectors {
        test ripemd128-tcl-3.$n {HMAC RIPEMD-128 test vectors (pure-Tcl impl)} {
            ::ripemd::hmac128 -hex -key $key -- $msg
        } $hash
    }
    set ::ripemd::ripemd128::usetrf 1
}

# -------------------------------------------------------------------------
# RFC 2286 has a set of test vectors for HMAC-RIPEMD168 and 128.
# This is those test vectors...
# -------------------------------------------------------------------------

set vectors \
    [list \
         1 [string repeat \x0b 16] "Hi There" \
         FBF61F9492AA4BBF81C172E84E0734DB \
         2 "Jefe" "what do ya want for nothing?" \
         875F828862B6B334B427C55F9F7FF09B \
         3 [string repeat \xaa 16] [string repeat \xdd 50] \
         09F0B2846D2F543DA363CBEC8D62A38D \
         4 \
         [binary format H* 0102030405060708090a0b0c0d0e0f10111213141516171819]\
         [string repeat \xcd 50] \
         BDBBD7CF03E44B5AA60AF815BE4D2294 \
         5 [string repeat \x0c 16] "Test With Truncation" \
         E79808F24B25FD031C155F0D551D9A3A \
         6 [string repeat \xaa 80] \
         "Test Using Larger Than Block-Size Key - Hash Key First" \
         DC732928DE98104A1F59D373C150ACBB \
         7 [string repeat \xaa 80] \
         "Test Using Larger Than Block-Size Key and Larger Than One Block-Size Data" \
         5C6BEC96793E16D40690C237635F30C5 \
        ]
foreach {n key msg hash} $vectors {
    test ripemd128-4.$n {RFC2286 test vectors for HMAC-RIPEMD128} {
        ::ripemd::hmac128 -hex -key $key -- $msg
    } $hash
}

if {$::ripemd::ripemd128::usetrf} {
    set ::ripemd::ripemd128::usetrf 0
    foreach {n key msg hash} $vectors {
        test ripemd128-tcl-4.$n {RFC2286 test vectors for HMAC-RIPEMD128 pure-Tcl} {
            ::ripemd::hmac128 -hex -key $key -- $msg
        } $hash
    }
    set ::ripemd::ripemd128::usetrf 1
}    

# -------------------------------------------------------------------------

test ripemd128-5.1 {Check hashing data that begins with hyphen} {
    list [catch {::ripemd::ripemd128 -hex -hello} msg] $msg
} {0 4FA192692A0FBB0C5E3092F8266A4014}

test ripemd128-5.2 {Check hashing data that begins with hyphen} {
    list [catch {::ripemd::ripemd128 -hex -- -hello} msg] $msg
} {0 4FA192692A0FBB0C5E3092F8266A4014}

test ripemd128-5.3 {Check hashing data that begins with hyphen} {
    list [catch {::ripemd::ripemd128 -hex --} msg] $msg
} {0 49D7D0E80B035C0EC8FFB81D07185B69}

test ripemd128-5.4 {Check hashing data that begins with hyphen} {
    list [catch {::ripemd::ripemd128 -hex -- --} msg] $msg
} {0 49D7D0E80B035C0EC8FFB81D07185B69}

test ripemd128-6.1 {Check hmac data that begins with hyphen} {
    list [catch {::ripemd::hmac128 -hex -key "" -hello} msg] $msg
} {0 D34FADEBBDAC6991C4CFB6D9C490AC1D}

test ripemd128-6.2 {Check hmac data that begins with hyphen} {
    list [catch {::ripemd::hmac128 -hex -key "" -- -hello} msg] $msg
} {0 D34FADEBBDAC6991C4CFB6D9C490AC1D}

test ripemd128-6.3 {Check hmac data that begins with hyphen} {
    list [catch {::ripemd::hmac128 -hex -key "" --} msg] $msg
} {0 3EA28051102F1F44AF3850FAD0EFCA13}

test ripemd128-6.4 {Check hmac data that begins with hyphen} {
    list [catch {::ripemd::hmac128 -hex -key "" -- --} msg] $msg
} {0 3EA28051102F1F44AF3850FAD0EFCA13}


# -------------------------------------------------------------------------

::tcltest::cleanupTests

# -------------------------------------------------------------------------
# Local Variables:
#   mode: tcl
#   indent-tabs-mode: nil
# End:

[vset VERSION 1.1]
[comment {-*- tcl -*- doctools manpage}]
[manpage_begin lazyset n [vset VERSION]]
[copyright {2018 Roy Keene}]
[moddesc   {Lazy evaluation for variables and arrays}]
[category  Utility]
[titledesc {Lazy evaluation}]
[require Tcl "8.5 9"]
[require lazyset [opt [vset VERSION]]]
[description]
[para]

The [package lazyset] package provides a mechanism for deferring code
execution until a specific variable or any index of an array is
referenced.

[para]
[emph {(Another way to defer execution is to create a singleton class.
The first time its constructor is called—if ever—the object will be
created. Any subsequent constructor call will get back the first created
object. See}]
[uri https://www.tcl-lang.org/man/tcl/TclCmd/singleton.html oo::singleton].)

[section {Commands}]

[list_begin definitions]
[call [cmd ::lazyset::variable] [opt [arg {-array boolean}]] \
[opt [arg {-appendArgs boolean}]] [arg someVar] [arg commandPrefix]]
Arrange for the code specified as [arg commandPrefix] to be executed when
the variable whose name is specified by [arg someVar] is read for
the first time.

[para]
If the optional argument [arg {-array boolean}] is specified as true,
then the variable specified as [arg someVar] is treated as an
array and attempting to read any index of the array causes that
index to be set by the [arg commandPrefix] as they are read.

[para]
If the optional argument [arg {-appendArgs boolean}] is specified as
false, then the variable name and subnames are not appended to the
[arg commandPrefix] before it is evaluated. 

[para]
If the argument [arg {-appendArgs boolean}] is not specified or is
specified as true, then 1 or 2 additional arguments are appended to the
[arg commandPrefix]. If [arg {-array boolean}] is specified as true,
then 2 arguments are appended corresponding to the name of the variable
and the index; otherwise 1 argument is appended containing the name of
the variable.

[para]
The [arg commandPrefix] code is run in the same scope as the variable
that is read.

[list_end]

[section Examples]

[example {
	::lazyset::variable page {apply {{name} {
		package require http
		set token [http::geturl http://www.tcl-lang.org/]
		set data [http::data $token]
		return $data
	}}}

	puts $page
}]

[example {
	::lazyset::variable -array true page {apply {{name index} {
		package require http
		set token [http::geturl $index]
		set data [http::data $token]
		return $data
	}}}

	puts $page(http://www.tcl-lang.org/)
}]

[example {
	::lazyset::variable -appendArgs false simple {
		return -level 0 42
	}

	puts $simple
}]

[section Authors]
Roy Keene

[vset CATEGORY utility]
[include ../common-text/feedback.inc]
[manpage_end]
